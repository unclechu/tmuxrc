#!/usr/bin/env perl
# Author: Viacheslav Lotsmanov
# License: Public Domain https://raw.githubusercontent.com/unclechu/tmuxrc/master/LICENSE
use v5.10; use strict; use warnings; use autodie qw(:all);

use Env qw(HOME TMUX);
use Cwd qw(abs_path);
use List::Util qw(first);
use File::Spec qw(devnull);
use File::Basename qw(basename);
use IPC::System::Simple qw(runx capturex);

use constant APP => basename $0;
use constant WUT => 'Something unexpectedly went wrong…';

use constant CMD_LIST =>
	qw(help cd nocd reconf colors sync nosync tsync 4 6 bar rc);

if (! defined($TMUX) || -z $TMUX) {
	say STDERR 'This app cannot be used outside of tmux session';
	exit 1;
}

sub get_usage {"Usage: @{[APP]} (@{[join '|', CMD_LIST]})"}

if (scalar(@ARGV) < 1) {
	say STDERR 'This app requires an argument';
	say STDERR get_usage;
	exit 1;
}

sub expand_cmd_by_prefix {
	my $partial = shift;
	die 'no cmd' unless defined $partial;
	my @matches = grep {$_ =~ /^$partial/} @_;

	if (scalar(@matches) == 1) {
		$matches[0]
	} else {
		say STDERR qq/Unknown command "$partial"/;

		say STDERR qq/Prefix "$partial" matches: /, join ', ', @matches
			if scalar(@matches) > 0;

		die 'unknown cmd'
	}
}

my $cmd = eval {expand_cmd_by_prefix shift, CMD_LIST};
if ($@) {say STDERR get_usage; exit 1}
my @cmd_pfx = (APP, $cmd);

sub wrong_cmd_args {
	my @args = do {my $x = shift; @{$x}};
	my $pfx = join ' ', @cmd_pfx;
	say STDERR qq/Incorrect arguments for `$pfx`: "@{[join ' ', @args]}"/;
	say STDERR "Usage: $pfx" . ((scalar(@_) > 0) ? (' ' . join ' ', @_) : '');
	exit 1;
}

sub expand_wrong_cmd_args {
	my $args = shift; # array by reference
	my $is_required = shift;
	my $partial = shift @{$args};
	my @cmd_list = @_;
	my @log_cmd_list = @cmd_list;

	if (!$is_required) {
		my $re = qr/^\*(.+)\*$/;

		unless (defined $partial) {
			$_ = first {/$re/} @cmd_list;
			if (defined) {s/$re/$1/; $partial = $_}
		}

		map {s/$re/$1/} @cmd_list;
	}

	my $cmd = (!$is_required && !defined $partial) ? undef :
		eval {expand_cmd_by_prefix $partial, @cmd_list};

	if ($@) {
		my $variants = join('|', @log_cmd_list);
		my @x = @{$args};
		unshift @x, $partial if defined $partial;
		wrong_cmd_args \@x, $is_required ? "($variants)" : "[($variants)]";
	}

	push @cmd_pfx, $cmd if defined $cmd;
	$cmd
}

sub get_env {
	my $env_var = shift;
	open OLD_STDERR, '<&STDERR';
	open STDERR, '>', File::Spec->devnull();
	my @get_cmd = qw(tmux showenv);
	push @get_cmd, '--', $env_var if defined $env_var;
	chomp(my $val = eval {capturex @get_cmd});
	open STDERR, '>&OLD_STDERR';
	$val =~ s/^$env_var=// if defined($env_var) && defined($val);

	unless (defined $env_var) {
		my %res = ();
		do {$res{$1} = $2 if /^([^=]+)=(.+)$/} for split '\n', $val;
		%res
	} else {
		$val
	}
}

sub reconf {runx qw(tmux source --), "$HOME/.tmux.conf"}

my %bar_env_var_map = (
	session     => '_TMUX_BAR_SESSION',
	socket      => '_TMUX_BAR_SOCKET',
	'pane-path' => '_TMUX_BAR_PANE_PATH',
);

my %bar_state_defaults = (
	session     => '1',
	socket      => '0',
	'pane-path' => '0',
);

sub get_last_bar_state {
	my $bar      = shift;
	my $prev_val = shift;

	$prev_val = get_env ($bar_env_var_map{$bar} || die WUT)
		unless defined $prev_val;

	my $get_default = sub {
		die WUT unless exists $bar_state_defaults{$bar};
		$prev_val = $bar_state_defaults{$bar};
	};

	$get_default->() if $@;
	die WUT if $prev_val ne '1' && $prev_val ne '0' && $prev_val ne '';
	return $get_default->() if $prev_val eq '';
	($prev_val eq '1') ? '1' : '0';
}

use constant COLORS_ENV_VAR => '_TMUX_COLORS';
use constant COLORS_DEFAULT => 'dark';

sub get_last_colors_state {
	my $state = get_env COLORS_ENV_VAR;
	$state = COLORS_DEFAULT if $@;
	die WUT if $state ne 'dark' && $state ne 'light' && $state ne '';
	$state = COLORS_DEFAULT if $state eq '';
	$state
}

if ($cmd eq 'help') {
	say get_usage;

} elsif ($cmd eq 'cd') {

	wrong_cmd_args \@ARGV, 'DIRECTORY' if scalar(@ARGV) > 1;
	my $arg;

	if (scalar(@ARGV) == 0) {
		say STDERR
			q/Received no directory value, /,
			q/interpreting this as HOME directory./;

		$arg = $HOME;
	} else {
		$arg = shift;
	}

	if ($arg eq '') {
		say STDERR
			q/Received empty directory value, /,
			q/unsetting previously set directory /,
			q/(if it have been set earlier)./;

		runx qw(tmux setenv -u -- _TMUX_CD);
		exit 0
	}

	my $dir = abs_path $arg;

	if (! -d $dir) {
		say STDERR
			qq/Cannot change directory to "$dir"/,
			q/ becuase it does not exists!/;

		exit 1
	}

	runx qw(tmux setenv -- _TMUX_CD), $dir;

} elsif ($cmd eq 'nocd') {
	wrong_cmd_args \@ARGV if scalar(@ARGV) != 0;
	runx qw(tmux setenv -- _TMUX_CD), '';

} elsif ($cmd eq 'reconf') {
	reconf;

} elsif ($cmd eq 'colors') {
	my $color = expand_wrong_cmd_args \@ARGV, 0, qw(dark light *toggle* show);

	if ($color eq 'show') {
		say get_last_colors_state;
		exit 0;
	}

	$color = (get_last_colors_state eq 'dark') ? 'light' : 'dark'
		if $color eq 'toggle';

	runx qw(tmux setenv --), COLORS_ENV_VAR, $color;
	reconf;

} elsif ($cmd eq 'sync') {
	runx qw(tmux setw synchronize-panes on);

} elsif ($cmd eq 'nosync') {
	runx qw(tmux setw synchronize-panes off);

} elsif ($cmd eq 'tsync') { # toggle
	runx qw(tmux setw synchronize-panes);

} elsif (($cmd eq '4') || ($cmd eq '6')) {

	wrong_cmd_args \@ARGV, 'SYNCHRONIZE-PANES'
		if (scalar(@ARGV) > 1) || ((scalar(@ARGV) == 1) && ($ARGV[0] ne '1'));

	my @preset = ($cmd eq '4') ?

		qw(splitw -h ; splitw -v ; selectp -t 1 ; splitw -v) :

		qw(
			splitw -h ; splitw -v ; splitw -v ;
			selectp -t 1 ; splitw -v ; splitw -v
		);

	@preset = ('tmux', @preset, qw(; selectl tiled ; selectp -t 1));
	my $sync_panes = (scalar(@ARGV) == 1) ? shift : undef;

	unless (defined $sync_panes) {runx @preset}
	else {runx @preset, qw(; setw synchronize-panes)}

} elsif ($cmd eq 'bar') {
	my $bar    = expand_wrong_cmd_args \@ARGV, 1, qw(session socket pane-path);
	my $action = expand_wrong_cmd_args \@ARGV, 0, qw(*toggle* on off show);

	if ($action eq 'show') {
		say get_last_bar_state $bar;
		exit 0;
	}

	my $to_state = sub {
		return '1' if $action eq 'on';
		return '0' if $action eq 'off';

		return (get_last_bar_state($bar) eq '1') ? '0' : '1'
			if $action eq 'toggle';

		die WUT
	}->();

	my $env_var = $bar_env_var_map{$bar} || die WUT;
	say ((($to_state eq '1') ? "Showing '" : "Hiding '")."$bar' bar…");
	runx qw(tmux setenv --), $env_var, $to_state;
	reconf;

} elsif ($cmd eq 'rc') {
	# helper for generating options for '.tmux.conf'

	my @commands = ();
	my $is_dark = (get_last_colors_state eq 'dark') ? 1 : 0;

	{
		my $wnd = $is_dark ?
			'bg=colour236,fg=colour187' : 'bg=colour254,fg=colour235';

		my $wnd_active = $is_dark ? 'bg=colour235' : 'bg=colour15';

		my @a = (qw(set -g @colors-solarized), ($is_dark ? 'dark' : 'light'));
		my @b = (qw(set -g window-style), $wnd);
		my @c = (qw(set -g window-active-style), $wnd_active);
		push @commands, \@a, \@b, \@c;
	}

	{
		my @a = (
			qw(set -g status-style),
			'bg=colour'.($is_dark ? 234 : 7).',fg=colour137,none'
		);

		my $windows_line_bg = $is_dark ? 'colour234' : 'colour7';
		my $date_time_fg    = $is_dark ? 'colour233' : 'colour254';

		my @b = (
			qw(set -g status-right),
			"#[bg=$windows_line_bg] "
				. "#[fg=$date_time_fg,bg=colour241,bold] %d/%m "
				. "#[bg=colour245] %H:%M:%S "
		);

		push @commands, \@a, \@b;
	}

	sub {
		my %tmux_env = get_env;

		my $get_last = sub {
			my $bar = shift;
			get_last_bar_state $bar, ($tmux_env{$bar_env_var_map{$bar}} || '')
		};

		my $is_session_on = $get_last->('session');
		my $is_socket_on  = $get_last->('socket');
		my $is_pane_path  = $get_last->('pane-path');

		my @bars = do {
			@_ = ();
			push @_, 'session_name'      if $is_session_on;
			push @_, 'socket_path'       if $is_socket_on;
			push @_, 'pane_current_path' if $is_pane_path;
			@_
		};

		if (scalar(@bars) == 0) {
			my @a = qw(set -g status-left-length 0);
			my @b = (qw(set -g status-left), '');
			push @commands, \@a, \@b;
			return;
		}

		my $one_bar_max_len = 22;
		my $status_left_max_len = ($one_bar_max_len + 2) * scalar(@bars) + 1;

		{
			my @x = (qw(set -g status-left-length), $status_left_max_len);
			push @commands, \@x;
		}

		my $status = do {
			my $l   = $one_bar_max_len;
			my $fg  = $is_dark ? 'colour233' : 'colour254';
			my $bgw = $is_dark ? 'colour234' : 'colour7'; # windows list bg
			my $bg1 = 'colour245';
			my $bg2 = 'colour241';

			if (scalar(@bars) == 3) {
				"#[fg=$fg,bg=$bg1,bold] #{=-$l:$bars[0]} "
					. "#[bg=$bg2] #{=-$l:$bars[1]} "
					. "#[bg=$bg1] #{=-$l:$bars[2]} "
					. "#[bg=$bgw] "
			} elsif (scalar(@bars) == 2) {
				"#[fg=$fg,bg=$bg1,bold] #{=-$l:$bars[0]} "
					. "#[bg=$bg2] #{=-$l:$bars[1]} #[bg=$bgw] "
			} elsif (scalar(@bars) == 1) {
				my $bg3 = $is_dark ? $bg1 : $bg2;
				"#[fg=$fg,bg=$bg3,bold] #{=-$l:$bars[0]} #[bg=$bgw] "
			} else {die WUT}
		};

		my @x = (qw(set -g status-left), $status);
		push @commands, \@x;
	}->();

	{
		my $x = $is_dark;

		my @a = (
			qw(set -g message-style),
			'bold,bg=colour'.($x ? 245 : 234).',fg=colour'.($x ? 234 : 7)
		);

		my @b = (
			qw(set -g message-command-style),
			'bold,bg=colour'.($x ? 234 : 245).',fg=colour'.($x ? 7 : 234)
		);

		push @commands, \@a, \@b;
	}

	my @commands_union = do {
		@_ = 'tmux';
		push @_, ';', @{$_} for @commands;
		splice @_, 1, 1;
		@_
	};

	runx @commands_union;

} else {die WUT}
